plugins {
    id 'com.android.application'
    id 'org.jetbrains.kotlin.android'
    id 'org.jetbrains.kotlin.kapt'
    id 'com.google.dagger.hilt.android'
    id 'org.jlleitschuh.gradle.ktlint'
}

android {
    compileSdk 33

    defaultConfig {
        minSdk 33
        targetSdk 33
        testInstrumentationRunner "androidx.test.runner.AndroidJUnitRunner"
    }

    signingConfigs {
        aosp {
            // Generated from the AOSP testkey:
            // https://android.googlesource.com/platform/build/+/refs/tags/android-11.0.0_r29/target/product/security/testkey.pk8
            keyAlias "testkey"
            keyPassword "testkey"
            storeFile file("testkey.jks")
            storePassword "testkey"
        }
    }
    buildTypes {
        debug {
            signingConfig signingConfigs.aosp
        }
        release {
            minifyEnabled false
            proguardFiles getDefaultProguardFile('proguard-android-optimize.txt'), 'proguard-rules.pro'
        }
    }

    compileOptions {
        sourceCompatibility JavaVersion.VERSION_11
        targetCompatibility JavaVersion.VERSION_11
    }
    kotlinOptions {
        jvmTarget = JavaVersion.VERSION_11.toString()
        languageVersion = "1.6"
    }

    lint {
        lintConfig file("lint.xml")
    }
}

kapt {
    correctErrorTypes true
}

apply from: '../gradle/dependencies.gradle'
dependencies {
    /**
     * Dependencies in AOSP
     *
     * We try to keep the dependencies in sync with what AOSP ships as SystemUpdater is meant to be
     * built with the AOSP build system and gradle builds are just for more pleasant development.
     * Using the AOSP versions in gradle builds allows us to spot issues early on.
     */
    compileOnly project.ext.aosp_libs
    implementation project.ext.std_libs.androidx_core_ktx
    implementation project.ext.std_libs.androidx_appcompat
    implementation project.ext.std_libs.androidx_fragment_ktx
    implementation project.ext.std_libs.androidx_activity_ktx
    implementation project.ext.google.guava
    implementation project.ext.google.gson
    implementation project.ext.google.com_google_android_material
    implementation project.ext.std_libs.androidx_lifecycle_viewmodel_ktx
    implementation project.ext.std_libs.androidx_preference
    implementation project.ext.std_libs.androidx_navigation_fragment_ktx
    implementation project.ext.std_libs.androidx_navigation_ui_ktx
    implementation project.ext.kotlinx.coroutines

    // Hilt is an exception due to lack of any specific version source
    // https://android.googlesource.com/platform/external/dagger2/+/refs/tags/android-13.0.0_r3
    def hilt_version = "2.44.2"
    kapt "com.google.dagger:hilt-compiler:$hilt_version"
    implementation "com.google.dagger:hilt-android:$hilt_version"
}
