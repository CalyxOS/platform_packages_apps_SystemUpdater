// To produce these binaries, in latest AOSP source tree, run
// $ m
ext.aosp_libs = fileTree(include: [
    // For more information about this module:
    // https://android.googlesource.com/platform/frameworks/base/+/refs/tags/android-11.0.0_r3/Android.bp#507
    // framework_intermediates/classes-header.jar works for gradle build as well,
    // but not unit tests, so we use the actual classes (without updatable modules).
    //
    // out/target/common/obj/JAVA_LIBRARIES/framework-minus-apex_intermediates/classes.jar
    'android.jar',
    // out/target/common/obj/JAVA_LIBRARIES/core-libart.com.android.art_intermediates/classes.jar
    'libcore.jar',
], dir: "$projectDir/libs")

ext.std_libs = [
    androidx_core: [
        // https://android.googlesource.com/platform/prebuilts/sdk/+/refs/tags/android-12.0.0_r2/current/androidx/Android.bp#867
        dependencies.create('androidx.core:core') {
            version { strictly '1.6.0' } // should be 1.6.0-beta03, but that is not even released, yet
        }
    ],
    // https://android.googlesource.com/platform/prebuilts/sdk/+/refs/tags/android-12.0.0_r2/current/androidx/Android.bp#195
    androidx_appcompat: dependencies.create('androidx.appcompat:appcompat') {
        version { strictly '1.3.0-rc01' } // should be 1.3.0-beta02, but that is not available via maven
    },
    // https://android.googlesource.com/platform/external/guava/+/refs/tags/android-12.0.0_r2/android/pom.xml
    guava: dependencies.create('com.google.guava:guava') {
        version { strictly '30.0-android' }
    }
]
